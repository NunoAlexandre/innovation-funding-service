*** Settings ***
Documentation     INFUND-3830: As a Competitions team member I want to view all competitions that are in the 'Live' state so I can keep track of them and access further details for each competition in that state
...
...               INFUND-3831 As a Competitions team member I want to view all competitions that are in ‘Project Setup’ so I can keep track of them and access further details for each competition in that state.
...
...               INFUND-3832 As a Competitions team member I want to view all competitions that are ‘upcoming’ so I can keep track of them and access further details for each competition in that state
Suite Setup       Guest user log-in    &{Comp_admin1_credentials}
Suite Teardown    the user closes the browser
Force Tags        CompAdmin    CompSetup
Resource          ../../../resources/GLOBAL_LIBRARIES.robot
Resource          ../../../resources/variables/GLOBAL_VARIABLES.robot
Resource          ../../../resources/variables/User_credentials.robot
Resource          ../../../resources/keywords/Login_actions.robot
Resource          ../../../resources/keywords/User_actions.robot
Resource          ../../../resources/keywords/SUITE_SET_UP_ACTIONS.robot

*** Test Cases ***
Live Competitions
    [Documentation]    INFUND-3830
    Given The user should see the text in the page    All competitions
    Then The user should see the text in the page    Open
    And the user should see the text in the page    Closed
    And the user should see the text in the page    Panel
    And the user should see the text in the page    Inform

Live competition calculations
    [Documentation]    INFUND-3830
    Then the calculations should be correct    Open    //section[1]/ul/li
    And the calculations should be correct    Closed    //section[2]/ul/li
    And the calculations should be correct    Panel    //section[3]/ul/li
    And the calculations should be correct    Inform    //section[4]/ul/li
    And the calculations should be correct    Live    //section/ul/li

Project setup Competitions
    [Documentation]    INFUND-3831
    When The user clicks the button/link    jQuery=a:contains(Project set up)    # We have used the JQuery selector for the link because the title will change according to the competitions number
    Then The user should see the text in the page    Project set up
    And The user should see the text in the page    Killer Riffs

Project setup competition calculations
    [Documentation]    INFUND-3831
    Then the calculations should be correct    Project set up    //section[1]/ul/li
    And the calculations should be correct    Project set up    //section/ul/li

Upcoming competitions
    [Documentation]    INFUND-3832
    When The user clicks the button/link    jQuery=a:contains(Upcoming)    # We have used the JQuery selector for the link because the title will change according to the competitions number
    Then The user should see the text in the page    In preparation
    And The user should see the text in the page    Ready to open

Upcoming competitions calculations
    [Documentation]    INFUND-3832
    Then the calculations should be correct    In preparation    //section[1]/ul/li
    And the calculations should be correct    Ready to open    //section[2]/ul/li
    And the calculations should be correct    Upcoming    //section/ul/li

*** Keywords ***
the calculations should be correct
    [Arguments]    ${Status}    ${Section_Xpath}
    [Documentation]    This keyword uses 2 arguments. The first one is about the status of the competition and the second is about the Xpath of the section.
    ${NO_OF_COMP}=    Get Matching Xpath Count    ${Section_Xpath}
    Page Should Contain    ${Status} (${NO_OF_COMP})
