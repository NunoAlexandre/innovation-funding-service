*** Settings ***
Suite Setup       Login as User    &{lead_applicant_credentials}
Suite Teardown    TestTeardown User closes the browser
Resource          ../../../resources/GLOBAL_LIBRARIES.robot
Resource          ../../../resources/variables/GLOBAL_VARIABLES.robot
Resource          ../../../resources/variables/User_credentials.robot
Resource          ../../../resources/keywords/Login_actions.robot
Resource          ../../../resources/keywords/Applicant_actions.robot

*** Test Cases ***
Test for the " Labour" calculations
    [Documentation]    INFUND-192
    [Tags]    Finance    Calculations    Labour    Applicant
    Given the applicant is in the "Your Finances" sub-section
    When the Applicant fills the Labour costs
    Then the calculations of the labour should be correct

Test for the "Materials" calculations
    [Documentation]    INFUND-192
    [Tags]    Finance    Calculations    Materials    Applicant
    Given the applicant is in the "Your Finances" sub-section
    When the Applicant fills the Materials fields
    Then the calculations of the Materials should be correct

Test for the "Subcontracting costs" calculations
    [Documentation]    INFUND-192
    [Tags]    Finance    Calculations    Subcontracting Costs    Applicant
    Given the applicant is in the "Your Finances" sub-section
    When the applicant edits the Subcontracting costs section
    The total subcontracting costs should be 100

Verify the fields that should be read-only are not editable
    [Documentation]    INFUND-192
    [Tags]    Finance    Calculations    Applicant
    Given the applicant is in the "Your Finances" sub-section
    When the Applicant is in the Labour sub-section
    Then the read-only fields are not editable

*** Keywords ***
the read-only fields are not editable
    Element Should Be Disabled    css=#labour-costs-table tbody td:nth-of-type(3) input
    Element Should Be Disabled    xpath=//*[@id="collapsible-1"]//td[contains(text(),"Total costs")]/input
    Element Should Be Disabled    xpath=//*[@id="collapsible-1"]//*[contains(text(), "Total labour costs")]//input

the Applicant fills the Labour costs
    Click Element    xpath=//*[@aria-controls="collapsible-1"]
    Click Element    link=Add another role
    Wait Until Page Contains Element    css=#labour-costs-table tbody td:nth-of-type(2) input
    Clear Element Text    css=#cost-labour-1-workingDays
    Input Text    css=#cost-labour-1-workingDays    230
    Input Text    css=#labour-costs-table tbody td:nth-of-type(2) input    120000
    Input Text    css=#labour-costs-table tbody td:nth-of-type(4) input    100
    Sleep    1s

the calculations of the labour should be correct
    Reload Page
    Textfield Value Should Be    css=#labour-costs-table tbody td:nth-of-type(3) input    £ 522
    Textfield Value Should Be    xpath=//*[@id="collapsible-1"]//td[contains(text(),"Total costs")]/input    £ 52,174

the Applicant fills the Materials fields
    Click Element    xpath=//*[@aria-controls="collapsible-3"]
    Click Element    link=Add another materials cost
    Wait Until Page Contains Element    css=#material-costs-table tbody td:nth-of-type(2) input
    Input Text    css=#material-costs-table tbody td:nth-of-type(2) input    10
    Input Text    css=#material-costs-table tbody td:nth-of-type(3) input    100

the calculations of the Materials should be correct
    Reload Page
    Confirm Action
    Textfield Value Should Be    css=#material-costs-table tbody td:nth-of-type(4) input    £ 1000
    Textfield Value Should Be    css=#material-costs-total-field    £ 1,000

the applicant edits the Subcontracting costs section
    Click Element    css=#question-20 > div.collapsible > h2:nth-child(9) > button
    Click Element    link=Add another subcontractor
    Wait Until Page Contains Element    css=[finance-subsection-table-container=32] \ > .form-row > div:nth-child(1) .form-finances-subcontracting-cost
    Input Text    css=[finance-subsection-table-container=32] \ > .form-row > div:nth-child(1) .form-finances-subcontracting-cost    100
    #Click Element    link=Add another subcontractor
    #Wait Until Page Contains Element    css=[finance-subsection-table-container=32] \ > .form-row > div:nth-child(2) .form-finances-subcontracting-cost
    #Input Text    css=[finance-subsection-table-container=32] \ > .form-row > div:nth-child(2) .form-finances-subcontracting-cost    100

The total subcontracting costs should be 100
    Textfield Value Should Be    css=#cost-subcontracting-total    £ 100

Input has value
    ${input_value} =    Get Value    ${input_selector}
    Should Be Equal As Strings    ${input_value}    £ 522
