*** Settings ***
Documentation     INFUND-887 : As an applicant I want the option to look up my business organisation's details using Companies House lookup so I donâ€™t have to type it in manually as part of the registration process
Suite Setup       Login as User    &{lead_applicant_credentials}
Suite Teardown    TestTeardown User closes the browser
Resource          ../../resources/GLOBAL_LIBRARIES.robot
Resource          ../../resources/variables/GLOBAL_VARIABLES.robot
Resource          ../../resources/variables/User_credentials.robot
Resource          ../../resources/keywords/Login_actions.robot
Resource          ../../resources/keywords/Applicant_actions.robot

*** Test Cases ***
Search using valid company name
    [Documentation]    INFUND-993 : Acceptance test: Search of the companies
    Given the user is in "Create your account" page
    When the user enters the valid company name in the Search text field
    Then the valid company names matching the search criteria should be displayed

Search using invalid company name
    Given the user is in "Create your account" page
    When the user enters the invalid company name in the Search text field
    Then the search criteria should not fetch any result

Search using valid registration number
    Given the user is in "Create your account" page
    When the user enters the valid registration number in the Search text field
    Then the valid company details matching the registration number should be displayed

search using invalid registration number
    Given the user is in "Create your account" page
    When the user enters the invalid registration number in the Search text field
    Then the search criteria should not fetch any result

*** Keywords ***
the user is in "Create your account" page
    go to    ${SEARCH_COMPANYHOUSE_URL}

the user enters the valid company name in the Search text field
    Input Text    id=org-name    innovateuk
    Click Element    id=org-search

the valid company names matching the search criteria should be displayed
    Page Should Contain Element    css=.type-company h3 a
    Click Link    INNOVATE UK LIMITED

the user enters the invalid company name in the Search text field
    Input Text    id=org-name    innoavate
    Click Element    id=org-search

the search criteria should not fetch any result
    Page Should Not Contain Element    css=.type-company h3 a

the user enters the valid registration number in the Search text field
    Input Text    id=org-name    05063042
    Click Element    id=org-search

the valid company details matching the registration number should be displayed
    Page Should Contain Element    css=.type-company p.meta.crumbtrail
    Click Link    INNOVATE UK LIMITED

the user enters the invalid registration number in the Search text field
    Input Text    id=org-name    87645
    Click Element    id=org-search
