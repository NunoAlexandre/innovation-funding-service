*** Settings ***
Documentation     INFUND-337
Resource          GLOBAL_LIBRARIES.txt
Resource          GLOBAL_VARIABLES.txt

*** Variables ***
${reject_application_name}    Analytical technologies for biopharmaceuticals
${accept_application_name}    Security for the Internet of Things
${competition_name}    Technology Inspired
${deadline_month}    december
${deadline_day}    31
${competitions_for_assessment_string}    Competitions for Assessment
${competition_details_page_title}    Competition Details
@{competitions_assessment_progress_before}    1    4
@{competitions_assessment_progress_after}    1    3
${partners_header_text}    Partners
${partner_to_check}    Ludlow

*** Test Cases ***
Assessment progress is 1 out of 4
    When Assessor is viewing the Competitions list
    Then Competitions progress should show    @{competitions_assessment_progress_before}

Assessor can see the applications details page
    [Documentation]    INFUND-337
    [Tags]    Assessor
    When Assessor clicks the competition
    Then Competition's details page should be visible

Applications details page has two lists
    [Documentation]    INFUND-322
    [Tags]    Assessor
    When Assessor is viewing the Competitions list
    When Assessor clicks the competition
    Then Details page should contain a list with the applications for assessment
    and Page should contain a list with the submitted assessments

Application invitation review page shows the title
    Given Assessor is viewing the Competitions Applications list
    When Assessor opens a pending application    ${accept_application_name}
    Then Application invitation Review page shows the Application title

Application invitation review page shows partners
    Given Assessor is viewing the Competitions Applications list
    When Assessor opens a pending application    ${accept_application_name}
    Then Application invitation Review page shows the Partners organisations

Application state changes when accepting an invitation for assessment
    [Tags]    Assessor
    Given Assessor is viewing the Competitions Applications list
    When Assessor opens a pending application    ${accept_application_name}
    and Assessor accepts the application
    Then Application status should change to open    ${accept_application_name}

Application state changes when rejecting an invitation for assessment
    [Tags]    Assessor
    Given Assessor is viewing the Competitions Applications list
    Given Assessor opens a pending application    ${reject_application_name}
    When Assessor rejects the application
    and Assessor enters the reason for rejection
    Then Application is not visible in the applications list    ${reject_application_name}

Application Summary shows your feedback when appropriate
    Given Assessor opens a pending application    ${accept_application_name}
    and Assessor clicks the Review Button
    When Assessor selects "No"
    Then Your feedback textarea appears
    When Assessor selects "Yes"
    Then Your feedback textarea disappears

Application Summary returns an error message when submitting empty feedback
    When Assessor selects "No"
    and Assessor save while Your Feedback is empty
    Then form will not be submitted

Assessor can see the competitions that he/she accepted
    When Assessor is viewing the Competitions list
    Then Assessor sees competitions he or she accepted

Competition has a deadline
    When Assessor is viewing the Competitions list
    Then Competition has a deadline

Competition has days remaining
    When Assessor is viewing the Competitions list
    Then Competition has a number of days remaining

Competition for Assessment count
    When Assessor is viewing the Competitions list
    Then Competitions for Assessment shows an amount of competitions

Assessment progress is 1 out of 3
    When Assessor is viewing the Competitions list
    Then Competitions progress should show    @{competitions_assessment_progress_after}

*** Keywords ***
Assessor clicks the competition
    Click Element    link=${competition_name}

Assessor clicks the Review Button
    Click Element    link=Review applications

Assessor selects "No"
    Select From List    xpath=//*[@name="is-suitable-for-funding"]    No

Your Feedback textarea appears
    Wait Until Element Is Visible    xpath=//*[@id="recommendation-feedback-group"]

Competition's details page should be visible
    Page Should Contain    ${competition_details_page_title}
    Capture Page Screenshot

Details page should contain a list with the applications for assessment
    Page Should Contain Element    css=#content > div.my-applications > div.in-progress

Assessor accepts the application
    Click Element    xpath=//button[@name="accept"]

Application status should change to open
    [Arguments]    ${application_name}
    Element Should Contain    xpath=//li[.//a[contains(text(),'${application_name}')]]//div[contains(@class,"open")]    Open

Assessor rejects the application
    Click Element    xpath=//*[@id="content"]//*[@class="application-summary"]//a[contains(text(),"Reject")]

Assessor enters the reason for rejection
    Input Text    id=rejection-observations    Test rejections
    Click Element    xpath=//*[@class="modal-reject-assessment"]//button[@name="reject"]

Page should contain a list with the submitted assessments
    Page Should Contain Element    css=#content > div.my-applications > div.submitted

Application is not visible in the applications list
    [Arguments]    ${application_name}
    Page Should Not Contain Element    xpath=//li//a[contains(text(),'${application_name}')]

Assessor opens a pending application
    [Arguments]    ${application_name}
    Click Element    xpath=//li//a[contains(text(),'${application_name}')]

Assessor is viewing the Competitions list
    Go To    ${SERVER}/assessor/dashboard

Assessor sees competitions he or she accepted
    Element Should Be Visible    xpath=//*[@class='my-applications' and .//*[contains(text(),"${competition_name}")]]

Competition has a deadline
    Element Should Be Visible    css=.competition-deadline
    Element Should Contain    css=.competition-deadline .day    ${deadline_day}
    Element Should Contain    css=.competition-deadline .month    ${deadline_month}

Competition has a number of days remaining
    ${number_of_days_element_text}=    Get Text    //*[@class='in-progress' and .//*[contains(text(),"Technology Inspired")]]//div[./span[contains(text(), "Days left")]]/div
    Should Match Regexp    ${number_of_days_element_text}    ^[0-9]{1,3}$

Competitions for Assessment shows an amount of competitions
    ${competitions_amount_element_text} =    Get Text    xpath=//*[contains(text(),'${competitions_for_assessment_string}')]
    Should Match Regexp    ${competitions_amount_element_text}    ${competitions_for_assessment_string} \\([0-9]+\\)

Assessor is viewing a Competition
    Go To    ${SERVER}/assessor/dashboard

Assessor is viewing the Competitions Applications list
    Go To    ${SERVER}/assessor/dashboard
    Click Element    link=${competition_name}

Competitions progress should show
    [Arguments]    @{assessment_progress}
    ${assessment_progress_element} =    Set Variable    //*[@class='in-progress' and .//*[contains(text(),"${competition_name}")]]//div[//*[contains(text(), "Assessment progress")]]/div/p/strong
    ${assessment_progress_assessed}=    Get Text    xpath=${assessment_progress_element}/span[1]
    ${assessment_progress_total}=    Get Text    xpath=${assessment_progress_element}/span[2]
    Should Be Equal As Integers    ${assessment_progress_assessed}    @{assessment_progress}[0]
    Should Be Equal As Integers    ${assessment_progress_total}    @{assessment_progress}[1]

Application invitation Review page shows the Application title
    Element Should Be Visible    xpath=//*[contains(text(),"${accept_application_name}")]

Application invitation Review page shows the Partners organisations
    Element Should Be Visible    xpath=//*[contains(text(),"${partners_header_text}")]/following-sibling::*[1]//*[contains(text(),"${partner_to_check}")]

Assessor selects "Yes"
    Select From List    xpath=//*[@name="is-suitable-for-funding"]    Yes

Your Feedback textarea disappears
    Wait Until Element Is Not Visible    xpath=//*[@id="recommendation-feedback-group"]

Assessor save while Your Feedback is empty
    Input Text    name=suitable-for-funding-feedback    ${EMPTY}
    Click Element    name=confirm-submission

Form will not be submitted
    Location Should Contain    summary
