import org.apache.tools.ant.filters.ReplaceTokens

apply plugin: 'docker'

buildscript {
    repositories { mavenCentral() }
    dependencies {
        classpath "se.transmode.gradle:gradle-docker:1.2"
    }
}

// if there is no key/cert provided, we generate a self-signed certificate
def String spProxyKeyFile = "${buildDir}/sp_proxy_key.pem"
def String spProxyCertificateFile = "${buildDir}/sp_proxy_certificate.pem"
def String spProxyCacertificateFile = "${buildDir}/sp_proxy_cacertificate.pem"

if ( project.hasProperty('auth.sp.proxy.key') ) { file(spProxyKeyFile).text = project['auth.sp.proxy.key'] }
if ( project.hasProperty('auth.sp.proxy.certificate') ) { file(spProxyCertificateFile).text = project['auth.sp.proxy.certificate'] }
if ( project.hasProperty('auth.sp.proxy.cacertificate') ) { file(spProxyCacertificateFile).text = project['auth.sp.proxy.cacertificate'] }

task prepareSecrets(type: Exec) {
    onlyIf { !file(spProxyKeyFile).exists() || !file(spProxyCertificateFile).exists() || !file(spProxyCacertificateFile).exists() }
    buildDir.mkdir()

    executable = 'openssl'
    args = ['req', '-x509',
            '-newkey', 'rsa:4096',
            '-keyout', spProxyKeyFile,
            '-out', spProxyCertificateFile,
            '-days', '3650',
            '-nodes',
            '-subj', '/C=GB/ST=Wiltshire/L=Swindon/O=Innovate UK/OU=IFS/CN=ifs.local-dev'
    ]
    doLast {
        copy {
            from file(spProxyCertificateFile)
            into buildDir
            rename 'sp_proxy_certificate.pem', 'sp_proxy_cacertificate.pem'
        }
    }
}

task prepareDockerFile(type: Copy) {
    dependsOn prepareSecrets
    outputs.upToDateWhen { false }

    from 'src/main/docker/'
    into 'src/main/docker/'
    include 'Dockerfile-template'
    rename 'Dockerfile-template', 'Dockerfile'
    filter(ReplaceTokens, tokens: [auth_service_trusted_ip: '-R \'127.0.0.1\''])
}

task buildDocker(type: Docker) {
    dependsOn prepareDockerFile

    push = false
    applicationName = 'ifs-sp-service'
    tagVersion = '1.0.0'
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from fileTree('src/main/docker/files')
            into "${stageDir}/files"
        }
        copy {
            from file(spProxyKeyFile)
            from file(spProxyCertificateFile)
            from file(spProxyCacertificateFile)
            into "${stageDir}/files/apache2"
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.0'
}
