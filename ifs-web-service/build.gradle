if (project.hasProperty("profile")) {
    apply from: "${project.profile}-build.gradle"
} else {
    apply from: "dev-build.gradle"
}

war {
    baseName = 'ifs-web-service'
    version = '0.0.1-SNAPSHOT'
}

bootRun {
    addResources = false
}

configurations {
    providedRuntime
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-thymeleaf"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity3"
    compile "org.thymeleaf.extras:thymeleaf-extras-java8time:2.1.0.RELEASE"
    compile "javax.servlet:javax.servlet-api:3.1.0"
    providedRuntime "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.projectlombok:lombok:1.16.2"
    compile "org.springframework.hateoas:spring-hateoas"
    compile "com.jayway.jsonpath:json-path"
    compile 'org.apache.commons:commons-lang3:3.4'

    // Use this for including the api lib from the data-service.
    compile files('libs/ifs-data-service-api-1.0-SNAPSHOT.jar')
    testCompile files('libs/ifs-data-service-tests-1.0-SNAPSHOT.jar')
}


task deployToTomcat(type: Copy) {
    from war
    into "$tomcatWebAppsDir"
    rename '(.*).war', 'ROOT.war'
}

task copyTemplatesInPlace(type: Copy) {
    into "$tomcatWebAppsDir/ROOT/WEB-INF/classes/templates"
    from 'src/main/resources/templates'
}

task copyCssInPlace(type: Copy) {
    into "$tomcatWebAppsDir/ROOT/WEB-INF/classes/static/css"
    from 'src/main/resources/static/css'
}

watch {

    java {
        files files('src/main/java')
        tasks('deployToTomcat')
    }

    templates {
        files files('src/main/resources/templates')
        tasks 'copyTemplatesInPlace'
    }

    css {
        files files('src/main/resources/static/css')
        tasks 'copyCssInPlace'
    }
}

processResources {
    filesMatching("*.properties") {
        expand project.properties
    }
    if (project.hasProperty("profile") && project.profile == "automated") {
        exclude '**/application.properties'
    }
}

task cleanDeploy() {
    build.mustRunAfter clean
    build.dependsOn clean
    deployToTomcat.dependsOn build
    dependsOn deployToTomcat

}
