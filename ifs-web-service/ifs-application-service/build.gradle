
bootRun {
    addResources = false
}

configurations {
    providedRuntime
}

jar {
    baseName = 'ifs-application-service'
}


bootRepackage {
    mainClass = 'org.innovateuk.ifs.Application'
    excludeDevtools = !(project.hasProperty('cloud') && cloud == 'development');
}

dependencies {
    compile project(':ifs-web-service:ifs-web-core')

    // weirdly, removing this line from this web module will cause the build to pull in version 2.1.2.RELEASE
    // despite 3.0.1.RELEASE being specified explicitly in ifs-web-core's build.gradle
    compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.1.RELEASE"
    compile "org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.0.RELEASE"

    testCompile project(':ifs-resources').sourceSets.test.output
    testCompile project(":ifs-commons").sourceSets.test.output
    testCompile project(':ifs-web-service:ifs-web-core').sourceSets.test.output

    testCompile "com.openpojo:openpojo:0.8.3"
    testCompile "org.skyscreamer:jsonassert:1.3.0"
    testCompile "net.sf.opencsv:opencsv:2.3"
}


processResources {
    filesMatching("*.properties") {
        expand project.properties
    }
}

processTestResources {
    filesMatching("*.properties") {
        expand project.properties
    }
}


task buildDocker( type:Docker, dependsOn: build) {
    push = false
    applicationName = jar.baseName.replaceAll(/ifs-/, "")
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

task cleanDeployServices() {
    build.mustRunAfter clean
    dependsOn clean
    dependsOn buildDocker
}

