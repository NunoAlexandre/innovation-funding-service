apply plugin: 'war'

war {
    baseName = 'management'
}

bootRepackage {
    mainClass = 'org.innovateuk.ifs.Application'
}

dependencies {
    compile project(':ifs-web-service:ifs-web-core')

    // weirdly, removing this line from this web module will cause the build to pull in version 2.1.2.RELEASE
    // despite 3.0.1.RELEASE being specified explicitly in ifs-web-core's build.gradle
    compile "org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.1.RELEASE"

    compile "commons-io:commons-io:2.4"

    testCompile project(':ifs-resources').sourceSets.test.output
    testCompile project(":ifs-commons").sourceSets.test.output
    testCompile project(':ifs-web-service:ifs-web-core').sourceSets.test.output

    testCompile "com.openpojo:openpojo:0.8.3"
    testCompile "org.skyscreamer:jsonassert:1.3.0"
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

processResources {
    filesMatching("*.properties") {
        expand project.properties
    }
    if (project.hasProperty("profile") && project.profile == "automated") {
        exclude '**/application.properties'
    }
}

task copyTemplatesInPlace(type: Copy) {
    into "$tomcatWebAppsDir/management/WEB-INF/classes/templates"
    from 'src/main/resources/templates'
}

task copyCssInPlace(type: Copy) {
    into "$tomcatWebAppsDir/management/WEB-INF/classes/static/css"
    from 'src/main/resources/static/css'
}

task copyJsInPlace(type: Copy) {
    into "$tomcatWebAppsDir/management/WEB-INF/classes/static/js/dest"
    from 'src/main/resources/static/js/dest'
}


task deployToTomcat(type: Copy) {
    from war
    into "$tomcatWebAppsDir"
    rename '(.*).war', 'management.war'
}

task buildDocker( type:Docker, dependsOn: build) {
    push = false
    applicationName = 'competition-management-service'
    dockerfile = file('src/main/docker/Dockerfile')
    doFirst {
        copy {
            from war
            into stageDir
        }
    }
}

task cleanDeploy() {
    build.mustRunAfter clean
    deployToTomcat.mustRunAfter build
    dependsOn clean
    dependsOn build
    dependsOn deployToTomcat
}

task cleanDeployServices() {
    build.mustRunAfter clean
    dependsOn clean
    dependsOn buildDocker
}

