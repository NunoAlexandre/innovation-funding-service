if (project.hasProperty("profile")) {
    apply from: "${project.profile}-build.gradle"
} else {
    apply from: "dev-build.gradle"
}

configurations {
    providedRuntime
    clientCompile
}

ext {
    javaLanguageLevel = 1.8
    sourceCompatibility = 1.8
}

bootRepackage {
    mainClass = 'com.worth.ifs.Application'
}

// Spring Boot 1.3 uses Spring REST Docs 1.0. To use 1.1, override the version
ext['spring-restdocs.version']='1.1.2.RELEASE'

dependencies {
    compile "org.springframework.boot:spring-boot-starter-data-jpa"
    compile "org.hibernate:hibernate-validator:5.2.4.Final"
    compile "org.springframework.security:spring-security-crypto"
    compile "org.springframework.boot:spring-boot-starter-security"
    compile "mysql:mysql-connector-java"
    compile "org.springframework:spring-web"
    compile "com.jayway.jsonpath:json-path-assert:2.0.0"
    compile "com.jayway.jsonpath:json-path"
    compile "org.springframework.statemachine:spring-statemachine-core:1.1.0.RELEASE"
    compile "org.springframework.statemachine:spring-statemachine-recipes-common:1.1.0.RELEASE"
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'commons-codec:commons-codec:1.10'
    compile "org.flywaydb:flyway-core:4.0"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    compile 'org.freemarker:freemarker:2.3.23'
    compile "org.apache.poi:poi:3.14"
    compile "org.apache.poi:poi-ooxml:3.14"

    compile 'javax.mail:mail:1.4.7'
    compile "org.apache.httpcomponents:httpasyncclient"
    compile "com.google.guava:guava:19.0"
    compile "commons-io:commons-io:2.4"
    compile 'org.jsoup:jsoup:1.9.1'
    compile "net.sf.opencsv:opencsv:2.3"

    compile files('../ifs-libs/ifs-data-service-public-1.0-SNAPSHOT.jar')
    testCompile files('../ifs-libs/ifs-data-service-tests-1.0-SNAPSHOT.jar')

    testCompile "org.springframework.restdocs:spring-restdocs-mockmvc:1.1.2.RELEASE"
    testCompile "com.openpojo:openpojo:0.8.3"
    testCompile "org.skyscreamer:jsonassert:1.3.0"
    testCompile 'org.powermock:powermock-api-mockito:1.6.5'
    testCompile 'org.powermock:powermock-module-junit4:1.6.5'

    testCompile group: 'org.hamcrest', name: 'hamcrest-core'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.springframework:spring-test'
    testCompile 'org.mockito:mockito-core'
    testCompile group: 'org.hamcrest', name: 'hamcrest-library'
}

task client(type: Jar) {
    baseName += '-public'
    from(sourceSets.main.output) {
        // filter to only include certain class files (Ant glob pattern)
        include "com/worth/ifs/Application.class"
        include "**/**.properties"
    }
}

task clientCopy(type: Copy){
    into "../ifs-libs/"
    from "build/libs/"
    include "ifs-rest-api-service*.jar"

}

//noinspection GroovyMissingReturnStatement
processResources {
    filesMatching("*.properties") {
        expand project.properties
    }
}

processTestResources {
    filesMatching("*.properties") {
        expand project.properties
    }
}


task cleanDeploy() {
    build.mustRunAfter clean
    clientCopy.mustRunAfter build
    dependsOn clean
    dependsOn build
    dependsOn clientCopy
}